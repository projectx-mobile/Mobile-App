plugins {
    id 'org.springframework.boot' version '2.7.3'
    id 'io.spring.dependency-management' version '1.0.13.RELEASE'
    id 'java'
    id 'org.liquibase.gradle' version '2.1.1'
}

dependencies {
    implementation project(':tasks')
    implementation project(':authorization')
    implementation project(':accounts')
    implementation project(':rewards')
    implementation project(':persistence')
    implementation project(':utils')
    implementation 'mysql:mysql-connector-java:8.0.30'
    compileOnly 'org.projectlombok:lombok:1.18.24'
    annotationProcessor 'org.projectlombok:lombok:1.18.24'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.liquibase:liquibase-core:4.15.0'
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    liquibaseRuntime 'org.liquibase:liquibase-groovy-dsl:3.0.2'
    liquibaseRuntime 'info.picocli:picocli:4.6.3'
    liquibaseRuntime group: 'javax.xml.bind', name: 'jaxb-api', version: '2.3.1'
    liquibaseRuntime sourceSets.main.output
    annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"
    testImplementation group: 'org.springframework.security', name: 'spring-security-test', version: '5.7.3'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.boot:spring-boot-starter-mail:2.7.4'
    testImplementation 'com.google.firebase:firebase-admin:9.0.0'
    testImplementation 'com.h2database:h2:2.1.214'
    testAnnotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.24'
    testCompileOnly 'org.projectlombok:lombok:1.18.24'
    implementation 'io.swagger:swagger-jaxrs:1.6.6'
    implementation 'com.amazonaws:aws-java-sdk-s3'
    implementation 'org.springdoc:springdoc-openapi-ui:1.6.11'
    implementation platform('com.amazonaws:aws-java-sdk-bom:1.12.307')
    testImplementation 'com.h2database:h2:2.1.214'
}

configurations {
    liquibaseRuntime.extendsFrom runtimeClasspath
}


liquibase {
    activities {
        main {
            changeLogFile "application/src/main/resources/db/changelog/db.changelog-master.yaml"
            url "jdbc:mysql://127.0.0.1:3306/kids_app"
            driver "com.mysql.cj.jdbc.Driver"
            username "root"
            password "12344321"
        }
    }
}


tasks.jar {
    enabled = false
}

tasks.bootJar {
    getArchiveBaseName().set 'kidsApp'
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
    all {
        exclude group: 'org.slf4j', module: 'slf4j-simple'
        exclude group: 'org.apache.logging.log4j', module: 'log4j-slf4j-impl'
    }
}

tasks.named('test') {
    useJUnitPlatform {
        includeTags "unit"
    }
}

tasks.register('integrationTest', Test) {
    description = 'Runs integration tests.'
    group = 'verification'
    useJUnitPlatform {
        includeTags "integration"
    }
}
